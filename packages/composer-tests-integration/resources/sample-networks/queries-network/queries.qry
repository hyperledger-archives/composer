/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/** Sample queries for Commodity Trading business network
*/

query selectCommodities {
  description: "Select all commodities"
  statement:
      SELECT org.acme.biznet.Commodity
}

query selectCommoditiesByExchange {
  description: "Select all commodities based on their main exchange"
  statement:
      SELECT org.acme.biznet.Commodity
          WHERE (mainExchange==_$exchange)
}

query selectCommoditiesByOwner {
  description: "Select all commodities based on their owner"
  statement:
      SELECT org.acme.biznet.Commodity
          WHERE (owner == _$owner)
}

query selectCommoditiesWithHighQuantity {
  description: "Select commodities based on quantity"
  statement:
      SELECT org.acme.biznet.Commodity
          WHERE (quantity > 60)
}

query selectCommoditiesOrdered {
  description: "Select commodities ordered by quantity"
  statement:
      SELECT org.acme.biznet.Commodity
      ORDER BY [quantity ASC]
}

query selectCommoditiesOrderedReverse {
  description: "Select commodities ordered by quantity"
  statement:
      SELECT org.acme.biznet.Commodity
      ORDER BY [quantity DESC]
}

query selectCommoditiesByDate {
  description: "Select commodities ordered by issue date"
  statement:
      SELECT org.acme.biznet.Commodity
      WHERE (quantity > 0)
      ORDER BY [issueDate ASC]
}

query selectCommoditiesByDateReverse {
  description: "Select commodities ordered by issue date"
  statement:
      SELECT org.acme.biznet.Commodity
      WHERE (quantity > 0)
      ORDER BY [issueDate DESC]
}

query selectCommoditiesWithMidQuantity {
  description: "Select commodities based on quantity and owner"
  statement:
      SELECT org.acme.biznet.Commodity
          WHERE (quantity >= 30 AND quantity < 60 AND owner == _$owner)
}

